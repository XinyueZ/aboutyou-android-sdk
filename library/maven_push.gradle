apply plugin: 'maven'
apply plugin: 'signing'

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                def homePath = System.properties['user.home']
                repository(url: "file://${homePath}/.sliceanddice-repo")
                uniqueVersion = false

                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.artifactId = POM_ARTIFACT_ID
                pom.groupId = POM_GROUP_ID
                pom.version = POM_VERSION

                pom.project {
                    name POM_NAME
                    packaging POM_PACKAGING
                }
            }
        }
    }

    signing {
        required { gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task androidJavadocs(type: Javadoc) {
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"

        source = android.sourceSets.main.allJava
        classpath += configurations.compile
        classpath += files(ext.androidJar)

        exclude '**/BuildConfig.java'
        exclude '**/R.java'

        options.showFromPublic()
        options.setWindowTitle("${POM_ARTIFACT_ID} ${POM_VERSION}")
    }

    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.allSource
    }

    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}